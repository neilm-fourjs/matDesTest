
ACTION DEFAULTS
	ACTION showform ( TEXT="Show Form", IMAGE="desktop" )
	ACTION constrct ( TEXT="Construct", IMAGE="database" )
	ACTION msg ( TEXT="Message", IMAGE="information" )
	ACTION err ( TEXT="Error", IMAGE="exclamation" )
	ACTION win ( TEXT="Model Window", IMAGE="circle" )
	ACTION win_mess ( TEXT="Message Window", IMAGE="comment" )
	ACTION win_quest ( TEXT="Question", IMAGE="question" )
	ACTION notify ( TEXT="Notify", IMAGE="circle" )
	ACTION wintitle ( TEXT="Window Title", IMAGE="square" )
	ACTION uitext ( TEXT="UI Text", IMAGE="square" )
	ACTION dyntext ( TEXT="Dynamic Text", IMAGE="square" )
	ACTION darklogo ( TEXT="Dark Logo", IMAGE="square" )
	ACTION lightlogo ( TEXT="Light Logo", IMAGE="square" )
	ACTION pg ( TEXT="ProgressBar", IMAGE="list" )
	ACTION pg50 ( TEXT="Prog Bar 50", IMAGE="battery-2" )
	ACTION inactive ( TEXT="Inactive", IMAGE="eye-slash" )
	ACTION fc_ismob ( TEXT="isMobile", IMAGE="mobile" )
	ACTION sleep ( TEXT="Sleep 5", IMAGE="bedtime" )
	ACTION clipset ( TEXT="Clipboard Set", IMAGE="copy" )
	ACTION clipshow ( TEXT="Clipboard Show", IMAGE="paste" )
	ACTION asterisk ( TEXT="Asterisk", ACCELERATOR="*" )
	ACTION asterisk2 ( TEXT="Asterisk2", ACCELERATOR="Shift-8" )
END -- AD

TOPMENU
	GROUP file (TEXT="File")
		COMMAND showform
		COMMAND inactive
		SEPARATOR
		COMMAND quit
	END -- FIle
	GROUP edit (TEXT="Edit")
		COMMAND editcut
		COMMAND editcopy
		COMMAND editpaste
	END -- FIle
	GROUP opt (TEXT="Option")
		COMMAND constrct
		COMMAND msg
		COMMAND err
		COMMAND win
		COMMAND win_mess
		COMMAND win_quest
		COMMAND notify
		COMMAND wintitle
		COMMAND uitext
		COMMAND dyntext
		COMMAND darklogo
		COMMAND lightlogo
		COMMAND pg
		COMMAND pg50
		COMMAND fc_ismob
		COMMAND sleep
		COMMAND clipset
		COMMAND clipshow
	END -- opt
	GROUP help (TEXT="Help")
		COMMAND about
	END -- opt
END -- TM

TOOLBAR
	ITEM quit
	SEPARATOR
	ITEM accept
	ITEM cancel
	SEPARATOR
	ITEM editcut
	ITEM editcopy
	ITEM editpaste
	SEPARATOR
	ITEM showform
	ITEM inactive
	ITEM msg
	ITEM err
	ITEM win_mess
	ITEM win
	ITEM pg
	ITEM pg50
	ITEM sleep
END -- TB

LAYOUT ( STYLE="main2", TEXT="Material Design Test.", IMAGE="bell" ) -- gbc_footer" )
VBOX ( STYLE="formborder" )
HBOX ( SPLITTER, ORIENTATION@SMALL = VERTICAL )
GRID
{
[lab1|f001       |lab2a|f02a  ]
[lab2|f002           |de1     ]
[lab3|f003           |de2     ]
[lab4|f004           |col     ]
[lab5|f005           |cl   ]
[lab6|f006           |     ]
[: :b1:but1  :msg    :err   : :]
<G g1                          >
[lab7|f007           ][a      ]
[lab8|f008           ][b      ]
[lab9|f009           ][c      ]
<                              >
 more text outside of group.
<T tab                         >
 [a001      |a002         |a03]
 [a001      |a002         |a03]
 [a001      |a002         |a03]
 [a001      |a002         |a03]
 [a001      |a002         |a03]
 [a001      |a002         |a03]
<                              >
}
END -- GRID

FOLDER (STYLE="accordion")

PAGE f2p1 (TEXT="Some Text", IMAGE="smiley")
GRID
{
This folder is using the 'accordion' style
IMAGEPATH:
[ip                                          ]
[                                            ]
[lfglpro                                     ]
[fglprof                                     ]
[fglpro                                      ]
[                                            ]
[                                            ]
[l4stfile         ]
[sf                                          ]
[                                            ]
[lrt         ]
[rtver                   ]
[lcli        ]
[client                                      ]
}
END -- GRID
END -- PAGE

PAGE f2p2 (TEXT="ScrollGrid", IMAGE="smiley", ACTION=arr2)
GRID
{
ScrollGrid with WANTFIXEDPAGESIZE=NO
<S tab2                                        >
 [t200|t202                                   ]
 [    |t201                                   ]
<                                              >
 [: :tab2info                              : :]
}
END -- GRID
END -- PAGE

PAGE f2p3 (TEXT="Table 3", IMAGE="smiley", ACTION=arr3)
GRID
{
<T tab3                                        >
 [t301      |t302                             ]
 [t301      |t302                             ]
 [t301      |t302                             ]
 [t301      |t302                             ]
 [t301      |t302                             ]
 [t301      |t302                             ]
 [t301      |t302                             ]
<                                              >
 [tab3info                                    ]
}
END -- GRID
END -- PAGE

PAGE f2p4 (TEXT="A Group", IMAGE="smiley")
GRID
{
<G "A Group"                               >
 Some static text
<                                          >
}
END -- GRID
END -- PAGE
END -- FOLDER

FOLDER (HIDDEN@SMALL)
PAGE p1 (TEXT="Page1", IMAGE="smiley")
GRID
{
Some text on a page.
------------------------
[""]
<G g2                   >
 text in a group
 Live Field: [p101     ]
 Dead Field: [p102     ]
[""]
 Table in a group:
 <T tab4               >
 [t402                ]
 [t402                ]
 [t402                ]
 [t402                ]
 [t402                ]
 [t402                ]
 [t402                ]
 [t402                ]
 [t402                ]
 [t402                ]
 <                     >
 bottom of group
<                       >
}
END -- GRID
END -- PAGE
PAGE p2 (TEXT="Page2", IMAGE="ssmiley")
GRID
{
page 2 is almost empty
<G "Logo in a group box"        >
 [logo2                        ]
 [                             ]
 [                             ]
}
END -- GRID
END -- PAGE
END -- FOLDER

END -- HBOX

HBOX (HIDDEN@SMALL)
GRID
{
[pg                         : :dcl         ]
}
END -- GRID
GRID
{
[sp]
}
END -- GRID
GRID
{
[logo]
}
END -- GRID
END -- HBOX

END -- VBOX
ATTRIBUTES

GROUP g1 : g1, TEXT=%"A Group #1", GRIDCHILDRENINPARENT,STYLE="uncollapsed curved";
GROUP g2 : g2, TEXT=%"A Group #2",STYLE="collapsed";

LABEL lab1 : lab1, TEXT=%"ButtonEdit:",JUSTIFY=RIGHT;
LABEL lab2 : lab2, TEXT=%"DateEdit:",JUSTIFY=RIGHT;
LABEL de1 : de1, TEXT=%"normal", STYLE="small";
LABEL de2 : de2, TEXT=%"dropdown", STYLE="small";
LABEL lab3 : lab3, TEXT=%"DateEdit:",JUSTIFY=RIGHT;
LABEL lab2a : lab2a, TEXT=%"Interval:",JUSTIFY=RIGHT;
LABEL lab4 : lab4, TEXT=%"Combo:",JUSTIFY=RIGHT;
LABEL lab5 : lab5, TEXT=%"Combo(QE):",JUSTIFY=RIGHT;
LABEL lab6 : lab6, TEXT=%"Completer:",JUSTIFY=RIGHT;
LABEL lab7 : lab7, TEXT=%"Readonly:",JUSTIFY=RIGHT;
LABEL lab8 : lab8, TEXT=%"Live Field:",JUSTIFY=RIGHT;
LABEL lab9 : lab9, TEXT=%"Dead Field:",JUSTIFY=RIGHT;
LABEL l4stfile : l4stfile, TEXT=%"Style File:";
LABEL lfglpro : lfglpro, TEXT=%"FGLPROFILE:";
LABEL lrt : lrt, TEXT=%"Runtime:";
LABEL lcli : lcli, TEXT=%"Client:";

BUTTONEDIT f001 = FORMONLY.fld1, ACTION=win, TABINDEX=2;
DATEEDIT f002 = FORMONLY.fld2, TABINDEX=3;
DATEEDIT f003 = FORMONLY.fld3, TABINDEX=4, STYLE="dropdown";
f02a = FORMONLY.fld2a, SCROLL, TABINDEX=5;
COMBOBOX f004 = FORMONLY.fld4, INITIALIZER=cb_colour, PLACEHOLDER=%"Pick a Colour", TABINDEX=6;
COMBOBOX f005 = FORMONLY.fld5, INITIALIZER=cb_colour, PLACEHOLDER=%"Pick a Colour", QUERYEDITABLE, TABINDEX=7;
f006 = FORMONLY.fld6,COMPLETER, TABINDEX=8;

WEBCOMPONENT col = FORMONLY.col_hex,
	STYLE="curvedborder",
	SIZEPOLICY=FIXED, 
	STRETCH=NONE, 
	SCROLLBARS=NONE, 
	HEIGHT=32 PIXELS, WIDTH=64 PIXELS,
	COMPONENTTYPE="colour";

WEBCOMPONENT cl = FORMONLY.cl, COMPONENTTYPE="clock";
WEBCOMPONENT dcl = FORMONLY.dcl, COMPONENTTYPE="dclock";

f007 = FORMONLY.fld7, NOENTRY;
f008 = FORMONLY.fld8, TABINDEX=9;
f009 = FORMONLY.fld9, NOENTRY;
p101 = FORMONLY.fld10, TABINDEX=10;
p102 = FORMONLY.fld11, NOENTRY;
CHECKBOX a = FORMONLY.okay, VALUECHECKED=1, VALUEUNCHECKED=0, TEXT=%"Ok?", NOT NULL, TABINDEX=11;
CHECKBOX b = FORMONLY.notokay, VALUECHECKED=1, VALUEUNCHECKED=0, TEXT=%"NOk?", NOT NULL, TABINDEX=12;
CHECKBOX c = FORMONLY.nul, VALUECHECKED=1, VALUEUNCHECKED=0, TEXT=%"Nul?";

-- normal table
--TABLE tab : tab, WANTFIXEDPAGESIZE; -- ideally want this for @small only!
a001 = FORMONLY.col1, TITLE="Fld1", IMAGECOLUMN=img;
a002 = FORMONLY.col2, TITLE="Fld2";
a03 = FORMONLY.col3, TITLE="Fld3";
PHANTOM FORMONLY.img;

BUTTON b1 : bomb, IMAGE="heart", TEXT="", TABINDEX=1;
BUTTON but1 : quit;
BUTTON msg : msg;
BUTTON err: err;

TEXTEDIT ip = FORMONLY.imgpath,STRETCH=X;
EDIT fglprof = FORMONLY.fglprof;
TEXTEDIT fglpro = FORMONLY.fglpro,STRETCH=X, STYLE="small";
TEXTEDIT sf = FORMONLY.stylefile,STRETCH=X;
EDIT rtver = FORMONLY.rtver;
EDIT client = FORMONLY.client;

-- list view table - old style
--TABLE tab2 : tab2, STYLE="listview";
--t201 = FORMONLY.lvcol1, SCROLL, IMAGECOLUMN=lvimg, STYLE="bold em12";
--t202 = FORMONLY.lvcol2, SCROLL,  STYLE="em8"; --, STYLE="html noborder", SCROLLBARS=NONE;
--PHANTOM FORMONLY.lvimg;
SCROLLGRID tab2 : tab2, STYLE="nocurrent", WANTFIXEDPAGESIZE=NO;
IMAGE t200 = FORMONLY.lvimg,AUTOSCALE, STYLE="noborder";
LABEL t201 = FORMONLY.lvcol1, STYLE="em8";
LABEL t202 = FORMONLY.lvcol2, STYLE="bold em11";

LABEL tab2info = FORMONLY.tab2info, STYLE="center";

-- arr2 table
TABLE tab3 : tab3, STYLE="table3";
t301 = FORMONLY.a3col1, SCROLL, IMAGECOLUMN=a3img, TITLE="Column 1";
TEXTEDIT t302 = FORMONLY.a3col2, SCROLL, TITLE="Column 2", SCROLLBARS=NONE, STYLE="html";
PHANTOM FORMONLY.a3img;
LABEL tab3info = FORMONLY.tab3info, STYLE="center";

-- table in group
TABLE tab4 : tab4, WANTFIXEDPAGESIZE, STYLE="nocurrent fill";
PHANTOM FORMONLY.tab401;
t402 = FORMONLY.tab402, TITLE="Colour";
PHANTOM FORMONLY.tab403;

IMAGE logo : logo, IMAGE="logo_dark", SIZEPOLICY=INITIAL,STYLE="noborder center";
IMAGE sp : sp, IMAGE="", STRETCH=X, STYLE="noborder";
PROGRESSBAR pg = FORMONLY.pg, VALUEMAX=10000 ;

IMAGE logo2 : logo2, IMAGE="logo_light", SIZEPOLICY=INITIAL,STYLE="noborder center";

INSTRUCTIONS
SCREEN RECORD arr1 [6] ( FORMONLY.col1 THRU FORMONLY.img )
--SCREEN RECORD arr2 ( FORMONLY.lvcol1 THRU FORMONLY.lvimg )
SCREEN RECORD arr2 ( FORMONLY.lvimg THRU FORMONLY.lvcol2 )
SCREEN RECORD arr3 ( FORMONLY.a3col1 THRU FORMONLY.a3img )
SCREEN RECORD arr4 ( FORMONLY.tab401 THRU FORMONLY.tab403 )
